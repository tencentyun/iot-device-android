apply plugin: 'java-library'
apply plugin: 'maven-publish'

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def libVersion = '1.0.0-SNAPSHOT'
publishing {
    publications {
        jar(MavenPublication) {
            groupId 'com.tencent.iot.explorer'
            artifactId 'explorer-device-java'
            version libVersion
            artifact(sourceJar)
            artifact(javadocJar)
            artifact file('build/libs/explorer-device-java.jar')
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = libVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("IOT_SONATYPE_USERNAME")
                password = System.getenv("IOT_SONATYPE_PASSWORD")
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    failOnError false
}

dependencies {
    implementation 'org.slf4j:slf4j-log4j12:1.8.0-alpha2'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.3'
    implementation 'org.json:json:20190722'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65'
    implementation 'net.iharder:base64:2.3.9'
    implementation 'com.google.code.gson:gson:2.8.6'
}

sourceCompatibility = "7"
targetCompatibility = "7"
